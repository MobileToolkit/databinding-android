plugins {
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.dcendents.android-maven' version '1.5'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName '0.1.0'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:${rootProject.ext.androidSupportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.androidSupportLibraryVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${rootProject.ext.kotlinVersion}"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${rootProject.ext.kotlinVersion}"
}

group = 'org.mobiletoolkit.android.databinding'
version = android.defaultConfig.versionName

install {
    repositories.mavenInstaller {
        pom.project {
            name project.name //'databinding'
            url 'https://github.com/MobileToolkit/databinding-android'
            inceptionYear '2018'

            packaging 'aar'
            groupId 'org.mobiletoolkit.android.databinding'
            artifactId project.name //'databinding'
            version android.defaultConfig.versionName
        }
    }
}

bintray {
    user = rootProject.ext.bintrayUser
    key = rootProject.ext.bintrayAPIKey
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'public'
        name = project.name //'databinding'
        userOrg = 'mobiletoolkit'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/MobileToolkit/databinding-android.git'

        version {
            name = android.defaultConfig.versionName
            released  = new Date()
            vcsTag = android.defaultConfig.versionName
            gpg {
                sign = true
                passphrase = rootProject.ext.bintrayGPGPassword
            }
        }
    }
}

